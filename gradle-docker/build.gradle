import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerRemoveContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

import java.nio.file.Paths

plugins {
    id "application"
    id "com.github.sherter.google-java-format" version "0.8"
    id "com.bmuschko.docker-remote-api" version "4.6.2"
}

repositories {
    mavenCentral()
}

sourceSets {
    intTest {}
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
    contrastAgent
}

def contrast_version = "3.6.3"
def contrast_build = "8220"

dependencies {
    compile "org.glassfish.grizzly:grizzly-http-server:2.4.4"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.4.1"
    testCompile "org.mockito:mockito-core:2.25.1"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.4.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.1"

    intTestImplementation "org.junit.jupiter:junit-jupiter-api:5.4.1"
    intTestRuntimeOnly "org.junit.platform:junit-platform-launcher:1.4.1"
    intTestImplementation "org.junit.jupiter:junit-jupiter-engine:5.4.1"
    contrastAgent "com.contrastsecurity:contrast-agent:${contrast_version}.${contrast_build}"
}

application {
    mainClassName = "com.contrastsecurity.examples.grizzly.App"

    def agentBuildPath = "lib/contrast-agent-${contrast_version}.jar"
    def agentProjectPath = Paths.get(getProjectDir().toURI()).resolve(agentBuildPath)


    applicationDefaultJvmArgs = [
            "-javaagent:${agentProjectPath.toString()}"
    ]

    distributions {
        main {
            contents {
                from("${projectDir}/lib") {
                    into "lib"
                }
            }
        }
    }

    startScripts {
        doLast {
            def shFile = new File(getOutputDir(), project.name)
            shFile.text = shFile.text.replace(agentProjectPath.toString(), "\$APP_HOME/${agentBuildPath}")
            def batFile = new File(getOutputDir(), "${project.name}.bat")
            batFile.text = batFile.text.replace(agentProjectPath.toString(), "%APP_HOME%\\lib\\contrast-agent-${contrast_version}.jar")
        }
    }
}

task createDockerfile(type: Dockerfile) {
    dependsOn assembleDist
    destFile = Paths.get("${buildDir}", "Dockerfile").toFile()
    from("openjdk:jre-alpine")
    workingDir("/")
    environmentVariable("CONTRAST_GRADLE_BIND_ADDR", "0.0.0.0")
    copyFile("distributions/${project.name}.zip", "dist.zip")
    runCommand("unzip dist.zip")
    workingDir("${project.name}")
    entryPoint("bin/${project.name}")
    exposePort(8080)
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = buildDir
}

task createContainer(type: DockerCreateContainer) {
    dependsOn buildDockerImage
    targetImageId buildDockerImage.getImageId()
    portBindings = ['8080:8080']

    envVars = [
            CONTRAST__API__URL         : System.getenv("CONTRAST__API__URL"),
            CONTRAST__API__USER_NAME   : System.getenv("CONTRAST__API__USER_NAME"),
            CONTRAST__API__SERVICE_KEY : System.getenv("CONTRAST__API__SERVICE_KEY"),
            CONTRAST__API__API_KEY     : System.getenv("CONTRAST__API__API_KEY"),
            CONTRAST__APPLICATION__NAME: "${project.name}-how-to"
    ]
}

task startContainer(type: DockerStartContainer) {
    dependsOn createContainer
    targetContainerId createContainer.getContainerId()
}

task removeContainer(type: DockerRemoveContainer) {
    removeVolumes = true
    force = true
    targetContainerId startContainer.getContainerId()
}

task integrationTest(type: Test) {
    description = "Test the application distributable."

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    useJUnitPlatform()

    dependsOn startContainer
    finalizedBy removeContainer
}

assemble.dependsOn integrationTest

test {
    useJUnitPlatform()
}

task copyAgent(type: Copy) {
    from configurations.contrastAgent
    into "${projectDir}/lib"
    rename "contrast-agent-*.*.*.jar", "contrast-agent-${contrast_version}.jar"
}

run.dependsOn copyAgent
assemble.dependsOn copyAgent

verifyGoogleJavaFormat {
    // ignore formatting errors in order to simplify the how-to
    ignoreFailures true
}
